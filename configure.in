# $Id$ 
AC_PREREQ(02.50)
AC_INIT([honeytrap], [0.6.5], [tillmann.werner@gmx.de])
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(honeytrap,0.6.5)

# Since we get -O2 from configure defaults, which doesn't work in 64bit
# mode, let's make some changes here before calling _CC macros.
AC_ARG_ENABLE(64bit-gcc,
[  --enable-64bit-gcc	   try to compile 64bit (only tested on Sparc Solaris 9).],
		[ CFLAGS="-O0 -g" CC="gcc -m64"; export CFLAGS CC ],)
# Disable annoying practice of recursively re-running the autotools
AM_MAINTAINER_MODE
AM_PROG_CC_STDC
AC_PROG_RANLIB
AC_PROG_CC
AC_PROG_LD(gnu-ld)
AC_PROG_LIBTOOL
 if test -n "$GCC"; then
      CFLAGS="$CFLAGS -Wall "
 fi

AC_ARG_ENABLE(debug,
[  --enable-debug          enable debugging options (bugreports and developers only)],
                [ if test -n "$GCC"; then
                    CFLAGS="-O0 -DDEBUG -g"
                  else
                    CFLAGS="$CFLAGS -DDEBUG"
                  fi      
                ], enable_debug="no")

AC_ARG_ENABLE(profile,
[  --enable-profile        enable profiling options (developers only)],
        [ if test -n "$GCC"; then
            CFLAGS="$CFLAGS -DPROFILE -pg"
          else
            CFLAGS="$CFLAGS -DPROFILE"
          fi
        ], enable_profile="no")

#AC_CANONICAL_HOST
linux=no
sunos4=no

AC_C_BIGENDIAN
SHELL="/bin/sh"

case "$host" in
  *-openbsd2.6|*-openbsd2.5|*-openbsd2.4|*-openbsd2.3*)
    AC_DEFINE(OPENBSD,,[Define if OPENBSD])
    AC_DEFINE(BROKEN_SIOCGIFMTU,,[Define if BROKEN_SIOCGIFMTU])

    ;;
  *-openbsd*)
    AC_DEFINE(OPENBSD)

    ;;
  *-sgi-irix5*)
    AC_DEFINE(IRIX,,[Define if IRIX])
    no_libsocket=yes
    no_libnsl=yes
    if test -z "$GCC"; then
      sgi_cc=yes
    fi
    LDFLAGS=${LDFLAGS} -L/usr/local/lib
    extra_incl=-I/usr/local/include
    ;;
  *-sgi-irix6*)
    AC_DEFINE(IRIX)
    no_libsocket=yes
    no_libnsl=yes
    if test -z "$GCC"; then
      sgi_cc=yes
    fi
    LDFLAGS=${LDFLAGS} -L/usr/local/lib
    extra_incl=-I/usr/local/include
    ;;
  *-solaris*)
    AC_DEFINE(SOLARIS,,[Define if SOLARIS])
    CPPFLAGS="${CPPFLAGS} -DBSD_COMP -D_REENTRANT"
    ;;
  *-sunos*)
    AC_DEFINE(SUNOS,,[Define if SUNOS])
    sunos4=yes
    ;;
  *-linux*)
    linux=yes
    AC_DEFINE(LINUX,,[Define if LINUX])
    # libpcap doesn't even LOOK at the timeout you give it under Linux
       AC_DEFINE(PCAP_TIMEOUT_IGNORED,,[Define if PCAP_TIMEOUT_IGNORED])
    AC_SUBST(extra_incl)
    extra_incl=-I/usr/include/pcap
    ;;
  *-hpux10*)
    AC_DEFINE(HPUX,,[Define if HPUX])
    AC_DEFINE(WORDS_BIGENDIAN)
    AC_SUBST(extra_incl)
    extra_incl=-I/usr/local/include
    ;;

  *-freebsd*)
    AC_DEFINE(FREEBSD,,[Define if FREEBSD])
    CFLAGS="$CFLAGS -fPIC -DPIC"

    ;;
  *-bsdi*)
    AC_DEFINE(BSDI,,[Define if BSDI])
    ;;
  *-aix*)
    AC_DEFINE(AIX,,[Define if AIX])
     broken_types=yes
    ;;
  *-osf4*)
    AC_DEFINE(OSF1,,[Define if OSF1])
    tru64_types=yes
    ;;
  *-osf5.1*)
    AC_DEFINE(OSF1)
    ;;
  *-tru64*)
    AC_DEFINE(OSF1)
    tru64_types=yes
    ;;
# it is actually <platform>-apple-darwin1.2 or <platform>-apple-rhapsody5.x but lets stick with this for the moment    
  *-apple*)
    AC_DEFINE(MACOS,,[Define if MACOS])
    AC_DEFINE(BROKEN_SIOCGIFMTU)
    LDFLAGS="${LDFLAGS} -L/sw/lib"
    extra_incl=-I/sw/include
esac

# any sparc platform has to have this one defined.
AC_MSG_CHECKING(for sparc alignment)
if eval "echo $host_cpu|grep -i sparc >/dev/null"; then
     AC_DEFINE(WORDS_MUSTALIGN,,[Define if WORDS_MUSTALIGN])
     AC_MSG_RESULT(yes)
else
     AC_MSG_RESULT(no)
fi

dnl checking headers
AC_CHECK_HEADERS(strings.h)
AC_CHECK_HEADERS(string.h)
AC_CHECK_HEADERS(stdlib.h)
AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADERS(sys/sockio.h)
AC_CHECK_HEADERS(pcap-bpf.h)
AC_CHECK_HEADERS(net/bpf.h)

dnl make sure we've got all our libraries
if test -z "$no_libnsl"; then
AC_CHECK_LIB(nsl, inet_ntoa)
fi

if test -z "$no_libsocket"; then
AC_CHECK_LIB(socket, socket)
fi


LIBS="${LIBS}"

# SunOS4 has several things `broken'
if test  "$sunos4" != "no"; then
AC_CHECK_FUNCS(vsnprintf,, LIBS=" $LIBS -ldb")
AC_CHECK_FUNCS(strtoul,, LIBS=" $LIBS -l44bsd")
fi

# some funky macro to be backwards compatible with earlier autoconfs
# in current they have AC_CHECK_DECLS

AC_DEFUN(SN_CHECK_DECL,[
AC_MSG_CHECKING([whether $1 must be declared])
AC_CACHE_VAL(sn_cv_decl_needed_$1,
[AC_TRY_COMPILE([
#include <stdio.h>
#ifdef HAVE_STRING_H
#include <string.h>
#endif
#ifdef HAVE_STRINGS_H
#include <strings.h>
#endif
#ifdef HAVE_STDLIB_H
#include <stdlib.h>
#endif
#ifdef HAVE_UNISTD_H
#include <unistd.h>
#endif
#include <sys/types.h>
#include <sys/socket.h>
#ifndef HAVE_PCAP_BPF_H
  #ifdef HAVE_NET_BPF_H
  #include <net/bpf.h>
  #endif
#else
#include <pcap.h>
#endif
],
[char *(*pfn) = (char *(*)) $1], 
eval "sn_cv_decl_needed_$1=no",eval "sn_cv_decl_needed_$1=yes") ])

if eval "test \"`echo '$sn_cv_decl_needed_'$1`\" != no"; then 
 AC_MSG_RESULT(yes)
 ifelse([$2], , :, [$2])
else
 AC_MSG_RESULT(no)
 ifelse([$3], , ,[$3])
fi
])dnl

AC_DEFUN(SN_CHECK_DECLS,
[for sn_decl in $1
do
sn_def_decl=`echo $sn_decl | tr [a-z] [A-Z]`
SN_CHECK_DECL($sn_decl,
[
AC_DEFINE_UNQUOTED(NEED_DECL_$sn_def_decl, 1,
                  [you have this cuz autoheader is dumb])
$2], $3)dnl
done
])

# some stuff for declarations which were missed on sunos4 platform too.
#
# add `#undef NEED_DECL_FUNCTIONAME to acconfig.h` because autoheader
# fails to work properly with custom macroses.
# you will see also #undef for each SN_CHECK_DECLS macros invocation
# because autoheader doesn't execute shell script commands.
# it is possible to make loops using m4 but the code would look even
# more confusing.. 
SN_CHECK_DECLS(printf fprintf fopen fclose fwrite fflush getopt \
           bzero bcopy memset strtol strerror perror socket sendto \
           snprintf strtoul)

AC_CHECK_FUNCS(snprintf)
AC_CHECK_FUNCS(strerror)

AC_TRY_COMPILE([
#include <stdio.h>
],[char *foo; foo = sys_errlist[0];], 
AC_DEFINE(ERRLIST_PREDEFINED,,[Define if ERRLIST_PREDEFINED]))

AC_MSG_CHECKING(for __FUNCTION__)
AC_TRY_COMPILE([
#include <stdio.h>
],[printf ("%s", __FUNCTION__);],
sn_cv_have___FUNCTION__=yes, sn_cv__have___FUNCTION__=no)
if test "x$sn_cv_have___FUNCTION__" = "xyes"; then
   AC_MSG_RESULT(yes)
   AC_DEFINE(HAVE___FUNCTION__, 1,
	     [Define if the compiler understands __FUNCTION__.])
else
   AC_MSG_RESULT(no)
   AC_MSG_CHECKING(for __func__)
   AC_TRY_COMPILE([
#include <stdio.h>
],[printf ("%s", __func__);],
sn_cv_have___func__=yes, sn_cv__have___func__=no)
   if test "x$sn_cv_have___func__" = "xyes"; then
      AC_MSG_RESULT(yes)
      AC_DEFINE(HAVE___func__, 1,
		[Define if the compiler understands __func__.])
      AC_DEFINE(__FUNCTION__, __func__, [Define __FUNCTION__ as required.])
   else
      AC_MSG_RESULT(no)
      AC_DEFINE(__FUNCTION__, "mystery function")
   fi
fi


AC_ARG_WITH(pcap-mon,
	[  --with-pcap-mon          Use libpcap to catch connection requests])
if test "$with_pcap_mon" = "yes"; then
  AC_ARG_WITH(libpcap_includes,
    [  --with-libpcap-includes=DIR  libpcap include directory],
    [with_libpcap_includes="$withval"],[with_libpcap_includes=no])

  AC_ARG_WITH(libpcap_libraries,
    [  --with-libpcap-libraries=DIR  libpcap library directory],
    [with_libpcap_libraries="$withval"],[with_libpcap_libraries=no])

  if test "$with_libpcap_includes" != "no"; then
     CPPFLAGS="${CPPFLAGS} -I${with_libpcap_includes}"
  fi
  AC_CHECK_HEADER(pcap.h,,[AC_ERROR(pcap.h not found.)])

  if test "$with_libpcap_libraries" != "no"; then
    LDFLAGS="${LDFLAGS}  -L${with_libpcap_libraries}"
  fi
  LPCAP=""
  AC_CHECK_LIB(pcap, pcap_datalink,, LPCAP="no")

  if test "$LPCAP" = "no"; then
    echo
    echo "   ERROR!  Libpcap library/headers not found, go get it from http://www.tcpdump.org"
    echo "   or use the --with-libpcap-* options, if you have it installed in unusual place."
    echo
    exit 1
  fi

  AC_DEFINE([USE_PCAP_MON], [], [Define if pcap connection monitor is used])
else with_pcap_mon="no"
fi

AC_ARG_WITH(ipq-mon,
	[  --with-ipq-mon          Use libipq to catch connection requests])
if test "$with_ipq_mon" = "yes"; then
  AC_ARG_WITH(libipq_includes,
    [  --with-libipq-includes=DIR  libipq include directory],
    [with_libipq_includes="$withval"],[with_libipq_includes=no])

  AC_ARG_WITH(libipq_libraries,
    [  --with-libipq-libraries=DIR  libipq library directory],
    [with_libipq_libraries="$withval"],[with_libipq_libraries=no])

  if test "$with_libipq_includes" != "no"; then
    CPPFLAGS="${CPPFLAGS} -I${with_libipq_includes}"
  fi
  AC_CHECK_HEADER(libipq.h,,[AC_ERROR(libipq.h not found.)])

  if test "$with_libipq_libraries" != "no"; then
    LDFLAGS="${LDFLAGS}  -L${with_libipq_libraries}"
  fi
  LIPQ=""
  AC_CHECK_LIB(ipq, ipq_set_mode,, LIPQ="no")

  if test "$LIPQ" = "no"; then
    echo
    echo "   ERROR!  Libipq library/headers not found, go get it from http://www.netfilter.org"
    echo "   or use the --with-libipq-* options, if you have it installed in unusual place."
    echo
    exit
  fi

  AC_DEFINE([USE_IPQ_MON], [], [Define if ip_queue connection monitor is used])
else with_ipq_mon="no"
fi

AC_ARG_WITH(nfq-mon,
	[  --with-nfq-mon           Use nfqueue to catch connection requests])
if test "$with_nfq_mon" = "yes"; then
  AC_ARG_WITH(libnetfilter_queue_includes,
    [  --with-libnetfilter_queue-includes=DIR  libnetfilter_queue include directory],
    [with_libnetfilter_queue_includes="$withval"],[with_libnetfilter_queue_includes=no])

  AC_ARG_WITH(libnetfilter_queue_libraries,
    [  --with-libnetfilter_queue-libraries=DIR  libnetfilter_queue library directory],
    [with_libnetfilter_queue_libraries="$withval"],[with_libnetfilter_queue_libraries=no])

  if test "$with_libnetfilter_queue_includes" != "no"; then
    CPPFLAGS="${CPPFLAGS} -I${with_libnetfilter_queue_includes}"
  fi
  AC_CHECK_HEADER(libnetfilter_queue/libnetfilter_queue.h,,[AC_ERROR(libnetfilter_queue.h not found.)])

  if test "$with_libnetfilter_queue_libraries" != "no"; then
    LDFLAGS="${LDFLAGS}  -L${with_libnetfilter_queue_libraries}"
  fi
  LNFQ=""
  AC_CHECK_LIB(netfilter_queue, nfq_open,, LNFQ="no")

  if test "$LNFQ" = "no"; then
    echo
    echo "   ERROR!  Libnetfilter_queue library/headers not found, go get it from http://www.netfilter.org"
    echo "   or use the --with-libnetfilter_queue-* options, if you have it installed in unusual place."
    echo
    exit
  fi
  AC_DEFINE([USE_NFQ_MON], [], [Define if netfilter_queue connection monitor is used])
else with_nfq_mon="no"
fi

AC_ARG_WITH(ipfw-mon,
	[  --with-ipfw-mon          Use ipfw to catch connection requests])
if test "$with_ipfw_mon" = "yes"; then
  echo "ipfw connection monitor is currently not supported."
  exit 1
  AC_DEFINE([USE_IPFW_MON], [], [Define if ipfw connection monitor is used])
else with_ipfw_mon="no"
fi

default_directory="/usr /usr/local"

AC_DEFUN(FAIL_MESSAGE,[
   echo
   echo
   echo "**********************************************"
   echo "  ERROR: unable to find" $1
   echo "  checked in the following places"
   for i in `echo $2`; do
     echo "        $i"
   done
   echo "**********************************************"
   echo
   exit 1
])

AC_CHECK_LIB(dl, dlsym,, DLLIB="no")
if test "$DLLIB" != "no"; then
	LIBS="$LIBS -ldl"
	LDFLAGS="$LDFLAGS -Wl,--export-dynamic"
	else
	AC_CHECK_LIB(c, dlsym,, DLCLIB="no")
	if test "$DLCLIB" = "no"; then
		echo
		echo "   ERROR!  programmatic interface to dynamic link loader"
		echo "   not found.  Cannot use dynamic plugin libraries."
		echo
		exit 1
	fi
fi


if test "$tru64_types" = "yes"; then
        AC_CHECK_TYPE(u_int8_t, unsigned char)
        AC_CHECK_TYPE(u_int16_t, unsigned short)
        AC_CHECK_TYPE(u_int32_t, unsigned int)
else
	if test "$broken_types" = "yes" ; then
		AC_CHECK_TYPE(u_int8_t, unsigned char)
		AC_CHECK_TYPE(u_int16_t, unsigned short)
		AC_CHECK_TYPE(u_int32_t, unsigned long int)
	else
		AC_CHECK_TYPE(u_int8_t, uint8_t)
		AC_CHECK_TYPE(u_int16_t, uint16_t)
		AC_CHECK_TYPE(u_int32_t, uint32_t)
	fi
fi

# Check for electric fence malloc debugger
AC_ARG_WITH(efence,
	[  --with-efence           link with electric fence ])
	if test "$with_efence" = "yes"
	then
		AC_CHECK_LIB(efence, EF_ALIGNMENT, LIBS="${LIBS} -lefence", AC_MSG_ERROR(libefence not found))
	else with_efence="no"
	fi


# let's make some fixes..

CFLAGS=`echo $CFLAGS |sed -e 's/-I\/usr\/include //g'`
CPPFLAGS=`echo $CPPFLAGS | sed -e 's/-I\/usr\/include //g'`

INCLUDES='-I$(top_srcdir) -I$(top_srcdir)/src'

AC_SUBST(INCLUDES)

if test $with_ipq_mon != "yes" -a $with_nfq_mon != "yes" -a $with_ipfw_mon != "yes" -a $with_pcap_mon != "yes"; then
  echo
  echo "   ERROR! No connection monitor selected. Please reconfigure with --with-pcap-mon or --with-ipq-mon."
  echo
  exit 1
fi


AC_PROG_INSTALL
AC_CONFIG_FILES([Makefile
		doc/Makefile
		src/Makefile
		src/modules/Makefile]) 

AC_OUTPUT

echo
echo "--- honeytrap configuration ---"
echo "Debugging options:             $enable_debug"
echo "Profiling options:             $enable_profile"
echo "Electric Fence:                $with_efence"
echo
echo "Connection monitor"
echo "  Linux ip_queue (ipq):        $with_ipq_mon"
echo "  Linux nfnetlink_queue (nfq): $with_nfq_mon"
echo "  FreeBSD ipfw (ipfw):         $with_ipfw_mon"
echo "  Libpcap (pcap):              $with_pcap_mon"
